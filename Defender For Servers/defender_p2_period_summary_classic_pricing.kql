// ============================================================================
// Defender for Servers P2 — PERIOD SUMMARY (Classic Pricing Edition)
// (Author: Venicia Solomons)
// ============================================================================
// One summary row for the entire lookback window showing totals:
//   Defender P2 savings, M365 E5 Sentinel savings, billable cost,
//   and what cost would have been without any benefits.
//
// HOW TO FIND YOUR RATES:
//   Go to Sentinel > Settings > Pricing tab.
//   LA rate  = LA tier daily cost ÷ tier GB  (e.g. $799.18 ÷ 400 = $2.00)
//   Sentinel rate = Sentinel tier daily cost ÷ tier GB (e.g. $892.32 ÷ 1000 = $0.89)
//
// CONFIGURATION:
let LookbackDays = 30;
let LAEffectiveRate = 2.00;       // Log Analytics effective $/GB from your commitment tier
let SentinelEffectiveRate = 0.89; // Sentinel effective $/GB from your commitment tier
let CombinedRate = 2.89;          // LA + Sentinel combined (for CostWithoutBenefit column)
let EligibleTables = dynamic([
    "SecurityEvent",
    "WindowsFirewall",
    "SecurityAlert",
    "SecurityBaseline",
    "SecurityBaselineSummary",
    "SecurityDetection",
    "ProtectionStatus",
    "Update",
    "UpdateSummary",
    "MDCFileIntegrityMonitoringEvents",
    "LinuxAuditLog"
]);
// -- Part A-1: Per-table daily ingestion in GB ---------------------------
let PerTableDaily =
    Usage
    | where TimeGenerated >= ago(1d * LookbackDays)
    | where IsBillable == true
    | where DataType in (EligibleTables)
    | summarize DailyMB = sum(Quantity) by Day = startofday(TimeGenerated), DataType
    | extend DailyGB = round(DailyMB / 1000.0, 3);
// -- Part A-2: Roll up to one row per Day --------------------------------
let DailyEligibleIngestion =
    PerTableDaily
    | summarize EligibleIngestionGB = round(sum(DailyGB), 3) by Day;
// -- Part B-1: Defender for Servers P2 benefit ---------------------------
let DailyDefenderBenefit =
    Operation
    | where TimeGenerated >= ago(1d * LookbackDays)
    | where Detail startswith "Benefit amount used"
    | parse Detail with "Benefit amount used: " BenefitGB_str " GB"
    | extend BenefitGB = toreal(BenefitGB_str)
    | parse OperationKey with "Benefit type used: " BenefitType
    | where BenefitType contains "MicrosoftDefender" or BenefitType contains "Node" or BenefitType contains "Standard" or BenefitType contains "Defender"
    | summarize DefenderBenefitGB = round(sum(BenefitGB), 3) by Day = startofday(TimeGenerated);
// -- Part B-2: Microsoft 365 E5 Sentinel benefit ------------------------
let DailySentinelBenefit =
    Operation
    | where TimeGenerated >= ago(1d * LookbackDays)
    | where Detail startswith "Benefit amount used"
    | parse Detail with "Benefit amount used: " BenefitGB_str " GB"
    | extend BenefitGB = toreal(BenefitGB_str)
    | parse OperationKey with "Benefit type used: " BenefitType
    | where BenefitType contains "Sentinel" or BenefitType contains "M365"
    | summarize SentinelM365BenefitGB = round(sum(BenefitGB), 3) by Day = startofday(TimeGenerated);
// -- Part C: Join, calculate daily, then summarise the period ------------
DailyEligibleIngestion
| join kind=leftouter DailyDefenderBenefit on Day
| join kind=leftouter DailySentinelBenefit on Day
| extend
    DefenderBenefitGB     = coalesce(DefenderBenefitGB, 0.0),
    SentinelM365BenefitGB = coalesce(SentinelM365BenefitGB, 0.0),
    BillableGB            = max_of(0.0, EligibleIngestionGB - coalesce(DefenderBenefitGB, 0.0))
| summarize
    PeriodDays                  = dcount(Day),
    TotalEligibleGB             = round(sum(EligibleIngestionGB), 2),
    TotalDefenderBenefitGB      = round(sum(DefenderBenefitGB), 2),
    TotalDefenderSavings        = round(sum(DefenderBenefitGB) * LAEffectiveRate, 2),
    TotalSentinelM365BenefitGB  = round(sum(SentinelM365BenefitGB), 2),
    TotalSentinelM365Savings    = round(sum(SentinelM365BenefitGB) * SentinelEffectiveRate, 2),
    TotalCombinedSavings        = round((sum(DefenderBenefitGB) * LAEffectiveRate) + (sum(SentinelM365BenefitGB) * SentinelEffectiveRate), 2),
    TotalBillableGB             = round(sum(BillableGB), 2),
    TotalBillableLACost         = round(sum(BillableGB) * LAEffectiveRate, 2),
    CostWithoutAnyBenefit       = round(sum(EligibleIngestionGB) * CombinedRate, 2),
    AvgDailyIngestionGB         = round(avg(EligibleIngestionGB), 3),
    AvgDailyDefenderSavings     = round(avg(DefenderBenefitGB) * LAEffectiveRate, 2),
    AvgDailyTotalSavings        = round((avg(DefenderBenefitGB) * LAEffectiveRate) + (avg(SentinelM365BenefitGB) * SentinelEffectiveRate), 2),
    AvgDailyBillableLACost      = round(avg(BillableGB) * LAEffectiveRate, 2),
    DaysBenefitActive           = countif(DefenderBenefitGB > 0),
    DaysBenefitInactive         = countif(DefenderBenefitGB == 0)
